*deck set2x.f
c***begin prologue     set2x
c***date written       951229   (yymmdd)
c***revision date      yymmdd   (yymmdd)
c***keywords           two-dim
c***author             schneider, barry (nsf)
c***source             3-dim
c***purpose            form two-dimensional hamiltonian in dvr representation
c***                   explicitly. the dynamical coordinates are (q1,q2) and
c***                   the third coordinate, q3, is constant.  
c***                   
c***                   
c***references         
c
c***routines called    
c***end prologue       set2x
      subroutine set2x(eig1,eig2,q3,ham,eig,vec,t1,t2,work,ipvt,
     1                 nply,npts,dim,nroots,pottyp,prnt,title)
      implicit integer (a-z)
      real*8 eig1, eig2, q3
      real*8 ham, t1, t2, work, eig, vec, tmp1, tmp2, tmp3
      character*(*) title
      character*(*) pottyp
      logical prnt
      dimension eig1(nply), eig2(nply), ham(dim,dim), eig(dim) 
      dimension t1(nply,nply), t2(nply,nply), work(dim,*)
      dimension vec(dim,nroots), ipvt(dim)
      common/io/inp, iout 
      write(iout,1) title
c     zero the hamiltonian matrix
      call rzero(ham,dim*dim)
c     put in the q1-coordinate part of the kinetic energy.
c     its diagonal in (q2)
      ii1=0
      do 10 i1=1,nply
         ii2=0
         do 20 i2=1,nply
            do 30 j1=1,nply
               indi=ii1 + j1
               indj=ii2 + j1
               ham(indi,indj) = ham(indi,indj) + t1(i1,i2)
 30         continue
            ii2=ii2+nply
 20      continue
         ii1=ii1+nply
 10   continue
c     put in the q2-coordinate part of the kinetic energy.
c     its diagonal in (q1)
      do 40 j1=1,nply
         do 50 j2=1,nply
            ii1=0
            do 60 i1=1,nply
               indi=ii1 + j1
               indj=ii1 + j2
               ham(indi,indj)=ham(indi,indj) + t2(j1,j2)
               ii1=ii1+nply
 60         continue
 50      continue
 40   continue
c     now add in the potential energy contribution.
c     the third coordinate is held constant at q3 on this particular face.
      if(pottyp.eq.'exponential') then
         tmp1=q3*q3
         ii1=0
         do 100 i1=1,nply
            tmp2=eig1(i1)*eig1(i1) + tmp1
            do 110 j1=1,nply
               tmp3 = sqrt( tmp2 + eig2(j1)*eig2(j1) )
               ind=ii1 + j1
               ham(ind,ind)=ham(ind,ind) + exp(-tmp3)
 110        continue
            ii1=ii1+nply
 100     continue
      elseif(pottyp.eq.'one') then   
         ii1=0
         do 200 i1=1,nply
            do 210 j1=1,nply
               ind=ii1 + j1
               ham(ind,ind) = ham(ind,ind) - 1.d0
 210        continue
            ii1=ii1+nply
 200     continue
      elseif(pottyp.eq.'coulomb') then
         tmp1=q3*q3
         ii1=0
         do 300 i1=1,nply
            tmp2=eig1(i1)*eig1(i1) + tmp1
            do 310 j1=1,nply
               tmp3 = tmp2 + eig2(j1)*eig2(j1)
               ind=ii1 + j1
               ham(ind,ind) = ham(ind,ind) - 1.d0/sqrt(tmp3)
 310        continue
            ii1=ii1+nply
 300     continue
      endif
      if(prnt) then
         title='2d-hamiltonian:dvr representation'   
         call prntrm(title,ham,dim,dim,dim,dim,iout)
      endif
      nn=nroots
      if(nroots.gt.dim) then
         nn=dim
      endif
      if(nn.eq.dim) then
         call tred2(dim,dim,ham,eig,work,ham)
         call tql2(dim,dim,eig,work,ham,ierr)
      else
          call tred1(dim,dim,ham,work(1,1),work(1,2),work(1,3))
          call imtqlv(dim,work(1,1),work(1,2),work(1,3),eig,
     1                ipvt,ierr,work(1,4))
          if(ierr.ne.0) then
             call lnkerr('error in diagonalization routine')
          endif
          call tinvit(dim,dim,work(1,1),work(1,2),work(1,3),nn,
     1                eig,ipvt,vec,ierr,work(1,4),work(1,5),
     2                work(1,6),work(1,7),work(1,8))             
          if(ierr.ne.0) then
             call lnkerr('error in diagonalization routine')
          endif
          call trbak1(dim,dim,ham,work(1,2),nn,vec)
      endif                   
      title='2d-eigenvalues:dvr representation'
      call prntrm(title,eig,nn,1,nn,1,iout)
 1    format(a80)
      return
      end       

