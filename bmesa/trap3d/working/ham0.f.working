*deck ham0.f
c***begin prologue     ham0
c***date written       960723   (yymmdd)
c***revision date      yymmdd   (yymmdd)
c***keywords           hamiltonian, one-particle
c***author             schneider, barry (nsf)
c***source             
c***purpose            one particle hamiltonian, eigenvalues
c***                                and eigenvectors.
c***                   
c***description        diagonalize one particle hamiltonian in 
c***                   polynomial basis.  the hamiltonian is
c                             2    2
c***                  ( - hbar )  d        
c                     (   ___  )  -    + v
c                     (   2*m  )    2  
c                                 dr    
c
c***                   v can be one of serveral interactions.  see
c***                   the if statements in the code
c***references         
c
c***routines called    
c***end prologue       ham0
      subroutine ham0(p,dp,ddp,q,wt,hamil,v,u,eig,tmp,hbar,mass,n,
     1                npts,lftbc,rtbc,coord,comp,prn)
      implicit integer (a-z)
      real*8 p, dp, ddp, q, wt, eig, hamil, v, u, hbar, mass
      real*8 tmp, tfac
      logical prn
c     logical dir
      character*80 title
      character*(*) coord, comp
      dimension p(npts,*), dp(npts,*), ddp(npts,*)
      dimension q(npts), wt(npts), hamil(n,n), v(*)
      dimension u(n,n), eig(n), tmp(*) 
      common/io/inp, iout
      add=0
      if(lftbc.eq.0) then
         add=1
      endif
      write(iout,1) coord, comp
      call rzero(hamil,n*n)
      tfac = hbar*hbar*.5d0/mass
      if(comp.eq.'x'.or.comp.eq.'y'.or.comp.eq.'z') then
         call tcart(p(1,add+1),dp(1,add+1),ddp(1,add+1),wt,
     1              hamil,n,npts)
      elseif(comp.eq.'rho') then
c
c        for cylindrical rho coordinate
c
         call tcylin(p(1,add+1),dp(1,add+1),ddp(1,add+1),q,wt,
     1               hamil,n,npts)
      elseif(comp.eq.'phi') then
c
c        for angular phi coordinate.  form is same as cartesian, just different
c        end points
c
         call tcart(p,dp,ddp,wt,hamil,n,npts)
      elseif(comp.eq.'r') then
c 
c        radial coordinate
c
         call tcart(p(1,add+1),dp(1,add+1),ddp(1,add+1),wt,
     1              hamil,n,npts)
      elseif(comp.eq.'theta') then
c        
c        spherical theta coordiante
c
         call tthet(p,dp,ddp,q,wt,hamil,tmp,n,npts)
      else
         call lnkerr('error in coordinate type')
      endif
      call vscale(hamil,hamil,tfac,n*n)
      call copy(hamil,u,n*n)
      do 10 i=1,n
         u(i,i) = u(i,i) + v(add+i)
 10   continue
      if (prn) then
          title='unperturbed hamiltonian'
          call prntfm(title,u,n,n,n,n,iout)
      endif
      call rdiag(u,eig,tmp,n)
      return
 1    format(/,5x,'forming a component of the unperturbed hamiltonian',
     1       /,5x,'coordinate system = ',a16,/,5x,'coordinate = ',a8)
      end       
